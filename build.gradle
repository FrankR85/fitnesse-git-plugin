
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

group = "org.fitnesse.plugins"
archivesBaseName = "fitnesse-git-plugin"
version = "0.1"


repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

sourceSets {
    main {
        java {
            srcDir 'src'

        }
    }
    test {
        java {
            srcDir 'test'
        }
    }
}

dependencies {
    compile "org.eclipse.jgit:org.eclipse.jgit:3.6.2.201501210735-r"
    compile "org.fitnesse:fitnesse:20150814"

    testCompile "junit:junit:4.12"
}

task("writeFitNesseClasspath", type: WriteFitNesseClasspath) {
    classpath = project.configurations.compile

    pagePath = project.file("FitNesseRoot/GradleClasspath")
}

task run(type: JavaExec) {
    dependsOn writeFitNesseClasspath
    dependsOn compileJava
    classpath configurations.compile
    main "fitnesseMain.FitNesseMain"
    args "-p", "8000", "-e", "0"
}

task intTest(type: JavaExec) {
    dependsOn writeFitNesseClasspath
    dependsOn compileJava
    dependsOn test
    classpath configurations.compile
    main "fitnesseMain.FitNesseMain"
    args "-c", "GitFileController?suite&format=text"
}

check.dependsOn intTest

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'fitnesse-git-plugin'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Git version controller support for FitNesse'
                url 'https://github.com/amolenaar/fitnesse-git-plugin'

                scm {
                    connection 'scm:git:git@github.com/amolenaar/fitnesse-git-plugin.git'
                    developerConnection 'scm:git:git@github.com/amolenaar/fitnesse-git-plugin.git'
                    url 'http://github.com/amolenaar/fitnesse-git-plugin'
                }

                licenses {
                    license {
                        name 'Common Public License version 1.0'
                        url 'http://www.opensource.org/licenses/cpl1.0'
                    }
                }

                developers {
                    developer {
                        id 'amolenaar'
                        name 'Arjan Molenaar'
                        email 'gaphor@gmail.com'
                    }
                }
            }
        }
    }
}


/// === Custom Tasks

class WriteFitNesseClasspath extends DefaultTask {
    @Input
    def classpath

    @OutputDirectory
    File pagePath

    @TaskAction
    def generatePage() {
        def contentTxt = project.file("${pagePath}/content.txt")
        contentTxt.createNewFile()
        contentTxt.withWriter { writer ->
            writer.writeLine("!path ${project.sourceSets.main.output.classesDir}")
            writer.writeLine("!path ${project.sourceSets.test.output.classesDir}")
            classpath.each { d ->
                writer.writeLine("!path $d")
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}
